X: 0 at ;
Y: 1 at ;
DX: 2 at ;
DY: 3 at ;
COLOR: 4 at ;
withX: swap 0 ins ;
withY: swap 1 ins ;
withDX: swap 2 ins ;
withDY: swap 3 ins ;
withColor: swap 4 ins ;

origo: [0 0] ;
add2Y: [ Y ] dip + withY  ;
add2X: [ X ] dip + withX ;

halfWidth: canvasWidth 2 /;
negativeHalfWidth: halfWidth -1 * ;
halfHeight: canvasHeight 2 / ;
negativeHalfHeight: halfHeight -1 * ;

topLeftCorner:
  []
  negativeHalfWidth swappend
  negativeHalfHeight swappend ;

fillBackground:
  "black" fillStyle
  topLeftCorner canvasWidth canvasHeight fillRect ;

randomWidth: negativeHalfWidth halfWidth random2 ;
randomVelocity: -2.5 2.5 random2 ;

colorwheel: ["teal" "blue" "pink" "yellow"] ;
randomColor: 0 colorwheel size random2 floor colorwheel of [ pop ] dip ;

randomPoint: randomWidth [0] cons ;
randomPointWithVelocity:
  randomPoint
  randomVelocity swappend
  randomVelocity swappend ;

randomPointWithVelocityAndColor:
  randomPointWithVelocity
  randomColor swappend ;

dot:
  beginPath
  0 2 PI * true arc
  fill
  closePath ;

smallDot: 1 dot ;
smallRedDot: "red" fillStyle smallDot ;

dots:
  []
  500 [ randomPointWithVelocityAndColor swons ] times ;

drawDots:
  dup
  [ COLOR fillStyle smallDot ] step ;

moveDots: [ DY add2Y DX add2X ] map ;

config:
  canvasWidth 2 / canvasHeight 2 / translate
  2.5 lineWidth
  1 globalAlpha ;

animate: 60 rAF ;

config
dots
fillBackground
0.25 globalAlpha
[ drawDots moveDots ] animate
