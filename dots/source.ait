"canvas" createElement canvasContext
500 500 canvasDimensions

.X: 0 at ;
.Y: 1 at ;
.DX: 2 at ;
.DY: 3 at ;
.C: 4 at ;
=X: 0 ins ;
=Y: 1 ins ;
=DX: 2 ins ;
=DY: 3 ins ;
=C: 4 ins ;

add2Y:
  -> P
  P .Y + P =Y ;
add2X:
  -> P
  P .X + P =X ;

point: [] cons cons ;

W: canvasWidth ;
H: canvasHeight ;
WMax: W 2 / ;
WMin: WMax neg ;
HMax: H 2 / ;
HMin: HMax neg ;
WindowMin: WMin HMin point ;

fillBackground:
  "black" fillStyle
  WindowMin W H fillRect ;

randomWidth: WMin WMax random2 ;
randomVelocity: -2.5 2.5 random2 ;

colors: ["teal" "blue" "pink" "yellow"] ;
takeRandom: dup size 0 swap random2 floor at ;

randomPoint: randomWidth 0 point ;
randomPointWithVelocity:
  randomVelocity randomPoint =DX
  randomVelocity swap =DY ;

randomPointWithVelocityAndColor:
  colors takeRandom randomPointWithVelocity =C ;

dot:
  beginPath
  0 2 PI * true arc
  fill
  closePath ;

smallDot: 1 dot ;

dots:
  []
  500 [ randomPointWithVelocityAndColor swons ] times ;

moveDot:
  dup .DY swap add2Y
  dup .DX swap add2X ;

draw:
  [ moveDot dup .C fillStyle smallDot ] step ;

centerCanvas: WMax WMax translate ;

centerCanvas
fillBackground
2.5 lineWidth
0.25 globalAlpha

dots -> Ds
[ Ds draw ] 60 animate
