hsl: "hsl(%s, %s%, %s%)" replace3 ;

teal: 60 85 180 hsl ;
black: 0 0 0 hsl ;
pink: "#d36fc8" ;
blue: "#2347e6" ;

Y: 1 at ;
X: 0 at ;
withX: 0 ins ;
withY: 1 ins ;

toCarthX:
  Y [ X ] dip cos * ;

toCarthY:
  Y [ X ] dip sin * ;

toCarth:
  toCarthX swap
  toCarthY swap withY
  withX ;

toPolarR:
  X dup * [ Y dup * ] dip + sqrt ;

toPolarPhi:
  Y [ X ] dip atan2 ;

toPolar:
  toPolarR swap
  toPolarPhi swap withY
  withX ;

toRad: 180 / PI * ;

2deg: PI / 180 * ;

add2Y: [ Y ] dip + swap withY ;
add2X: [ X ] dip + swap withX ;

halfWidth: canvasWidth 2 / ;
negativeHalfWidth: halfWidth -1 * ;
halfHeight: canvasHeight 2 / ;
negativeHalfHeight: halfHeight -1 * ;

topLeftCorner:
  []
  negativeHalfWidth swappend
  negativeHalfHeight swappend ;


fillBackground:
  black fillStyle
  topLeftCorner canvasWidth canvasHeight fillRect ;

random25: -25 25 random2 ;

calcNextControlPoint:
  random25 add2X
  3 toRad add2Y ;

calcNextAlongCircle:
  6 toRad add2Y ;

circle:
  beginPath
  dup [ toCarth moveTo ] dip
  [ Y 2 PI * 6 toRad - <= ]
  [
    dup [ calcNextControlPoint ] dip
    calcNextAlongCircle dup
    [ toCarth [ toCarth ] dip quadraticCurveTo ] dip
  ]
  while
  pop
  closePath ;

tealStroke: teal strokeStyle ;
pinkStroke: pink strokeStyle ;
blueStroke: blue strokeStyle ;

animate: 60 rAF ;

config:
  canvasWidth 2 / canvasHeight 2 / translate
  2.5 lineWidth
  0.5 globalAlpha
  black fillStyle ;

config
[ fillBackground
  [175 0] circle tealStroke stroke [] unstack
  [125 0] circle pinkStroke stroke [] unstack
  [150 0] circle blueStroke stroke [] unstack ]
animate
