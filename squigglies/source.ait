@load ait-canvas ;
@load ait-canvas/node ;
@load ./trig ;
@load ./colors ;

nodeCanvas canvasContext
500 500 canvasDimensions

add2Y:
  swap
  -> P
  P Y + P withY ;
add2X:
  swap
  -> P
  P X + P withX ;

H: canvasHeight ;
W: canvasWidth ;
WMax: W 2 / ;
WMin: WMax neg ;
HMax: H 2 / ;
HMin: HMax neg ;
WindowMin: WMin HMin [] cons cons ;
WindowMax: WMax HMax [] cons cons ;

fillBackground:
  black fillStyle
  WindowMin W H fillRect ;

random25: -25 25 random2 ;

calcNextControlPoint:
  random25 add2X
  3 toRad add2Y ;

calcNextAlongCircle:
  6 toRad add2Y ;

circle:
  beginPath
  dup toCarth moveTo
  [ Y 2 PI * 6 toRad - <= ]
  [
    dup [ calcNextControlPoint toCarth ] dip
    calcNextAlongCircle dup
    [ toCarth quadraticCurveTo ] dip
  ]
  while
  pop
  closePath ;

tealStroke: teal strokeStyle ;
pinkStroke: pink strokeStyle ;
blueStroke: blue strokeStyle ;

config:
  canvasWidth 2 / canvasHeight 2 / translate
  2.5 lineWidth
  1 globalAlpha
  black fillStyle ;

config
fillBackground
[175 0] circle tealStroke stroke [] unstack
[125 0] circle pinkStroke stroke [] unstack
[150 0] circle blueStroke stroke [] unstack

'squiggly.png' writeImage
